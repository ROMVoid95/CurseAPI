plugins {
	id "com.github.johnrengelman.shadow" version "6.1.0"
}

group = "com.therandomlabs.curseapi"
version = "1.0.0"

ext {
	commonGradleBranch = "master"
	defaultCompileDependencies = false
	jacocoMinimumInstructionCoverage = 0.9
	jacocoExcludes = [
		"com.therandomlabs.curseapi.CurseAPIProvider",
		"com.therandomlabs.curseapi.file.CurseAlternateFile",
		"com.therandomlabs.curseapi.forgesvc.*",
		"com.therandomlabs.curseapi.game.CurseGameVersion",
		"com.therandomlabs.curseapi.game.CurseGameVersionGroup",
		"com.therandomlabs.curseapi.util.*"
	]
}

apply from: "https://raw.githubusercontent.com/TheRandomLabs/Common-Gradle/${project.commonGradleBranch}/build.gradle"

def retrofitVersion = "2.9.0"
def resilience4jVersion = "1.7.0"

dependencies {

	implementation "com.google.guava:guava:${guavaVersion}"

	// https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
	// https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-api
	implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
	
	api "com.squareup.moshi:moshi:1.12.0"

	api "com.squareup.retrofit2:retrofit:${retrofitVersion}"
	implementation "com.squareup.retrofit2:converter-moshi:${retrofitVersion}"

	api "org.jsoup:jsoup:1.13.1"

	implementation("io.github.resilience4j:resilience4j-circuitbreaker:${resilience4jVersion}") {
		exclude module: "slf4j-api"
	}
	implementation("io.github.resilience4j:resilience4j-retry:${resilience4jVersion}"){
		exclude module: "slf4j-api"
	}
	implementation("io.github.resilience4j:resilience4j-retrofit:${resilience4jVersion}") {
		exclude module: "slf4j-api"
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = "CLASS"

			limit {
				counter = "INSTRUCTION"
				value = "COVEREDRATIO"
				minimum = 0.67
			}

			includes = [
				"com.therandomlabs.curseapi.forgesvc.*"
			]
		}
	}
}

javadoc.options.links.addAll([
	"https://square.github.io/moshi/1.x/moshi/",
	"https://square.github.io/okio/2.x/okio/",
	"https://square.github.io/okhttp/3.x/okhttp/",
	"https://square.github.io/retrofit/2.x/retrofit/",
	"https://www.javadoc.io/doc/org.jsoup/jsoup/latest/"
])

build.dependsOn shadowJar

shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
}
